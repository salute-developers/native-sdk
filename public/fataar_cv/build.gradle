plugins {
    id 'com.android.library'
    id 'com.kezong.fat-aar'
    id 'maven-publish'
}

apply from: "$rootProject.projectDir/android_subproject.gradle"

fataar {
    /**
     * If transitive is true, we embed not only our libs, but also their dependencies. It may be needed for unity build
     */
    transitive = false
}

afterEvaluate {
    // for app test, copy the final aar to demo app
    android.libraryVariants.all { variant ->
        File outputFile = variant.outputs.first().outputFile
        tasks.named("assemble${variant.name.capitalize()}").configure {
            doLast {
                copy {
                    from outputFile
                    into "$rootProject.projectDir/demo"
                    rename outputFile.name, "cv.aar"
                }
            }
        }
    }
}

dependencies {
    api "ru.sberdevices.smartapp:common_libs:1.0.1"

    embed project(path: ':public:cv:aidl', configuration: 'default')
    embed project(path: ':public:cv:api_cv', configuration: 'default')
    embed project(path: ':public:cv:entity', configuration: 'default')
    embed project(path: ':public:cv:impl', configuration: 'default')
    embed project(path: ':public:cv:proto', configuration: 'shadow')
    embed project(path: ':public:cv:util', configuration: 'default')
}

afterEvaluate {
    publishing {
        repositories.add(rootProject.repositories.getByName('OSSRH'))

        publications {
            MavenPublicationFatAarCV(MavenPublication) {
                artifact("$buildDir/outputs/aar/fataar_cv-release.aar")

                groupId publication.pomGroupID
                artifactId "cv"
                version publication.pomCvVersion

                pom {
                    name = "cv api"
                    description = 'cv helps recognize user’s gestures and pose using device’s camera'
                    url = publication.githubUrl
                    licenses {
                        license {
                            name = publication.licenseName
                            url = publication.licenseUrl
                        }
                    }
                    developers {
                        developer {
                            name = 'Irina Karpenko'
                            email = 'IViKarpenko@sberbank.ru'
                        }
                        developer {
                            name = 'Dmitry Levshunov'
                            email = 'Levshunov.D.A@sberbank.ru'
                        }
                    }
                    scm {
                        connection = publication.connectionUrl
                        developerConnection = publication.connectionUrl
                        url = publication.githubUrl
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    // Iterate over the compile dependencies (we don't want the test ones),
                    // adding a <dependency> node for each
                    configurations.api.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.MavenPublicationFatAarCV
    }
}